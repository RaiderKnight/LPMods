WHAT: none
WHERE: none
WHEN: 0 - 24
WHO: none
OTHER: none

    SceneStart()
    
    stayInMenu = true
    target = 0
    target2 = Player
    While stayInMenu
        clearGetList()  // reset the get list for each loop, otherwise we would not find the same contact again
        
        tg1 = "none"
        If target.isValid()
            If target.isSameAs( Player )
                tg1 = "PLAYER"
            Else
                tg1 = "<target.Name> <target.name_last>"
            EndIf
        EndIf
        
        tg2 = "none"
        If target2.isValid()
            If target2.isSameAs( Player )
                tg2 = "PLAYER"
            Else
                tg2 = "<target2.Name> <target2.name_last>"
            EndIf
        EndIf
        
        0:: false::"Targets: 1=[<tg1>] 2=[<tg2>]"
        1::"Select target 1"
        2::"Select target 2"
        3::false::"-------------------------"
        10:: target.isValid() && !target.isContactExchanged()::"Add <tg1> to contacts of PLAYER"
        3::false::"-------------------------"
        20:: "List relationships of PLAYER"
        21:: target.isValid():: "Modify PLAYER relation to <tg1> (NPCs only)"
        22:: target.isValid()&&target.isRelative():: "Modify PLAYER relation to <tg1> (Relatives only)"
        23:: target.isValid()&&!target.isRelative()&&target2.isValid():: "Change NPC to NPC relation"
        3::false::"-------------------------"
        41::"Batch processing / listing"
        3::false::"-------------------------"
        31::"Erase ALL NPCs"
        32::"Erase ALL NPCs (except Contacts)"
        33::"Erase ALL RELATIVES"
        3::false::"-------------------------"
        99::"EXIT"
        
        If 1 || 2 
            tgSlot = choice
            201::"Select contact"
            202::"Find NPC by name"
            203::"Find relative by name"
            204::"Select PLAYER"
            tgTemp = 0
            
            If 201
                selectNPC()
                tgTemp = getSelectedNPC()
            ElseIf 204
                tgTemp = Player
            ElseIf 202||203
                "Enter the name you are looking for or use *"
                tgName = enterString()
                iterate = true
                findAll = false
                
                If isSameString("", tgName)
                    iterate = false
                    "Aborting..."
                Else
                    If isSameString("*", tgName)
                        findAll = true  // wildcard
                    EndIf
                    
                    "Enter the last name or leave empty"
                    tgLastName = enterString()
                    doLastNameCheck = true
                    If isSameString("", tgLastName) || isSameString("*", tgLastName)
                        doLastNameCheck = false
                        "Looking for '<tgName>'."
                    Else
                        "Looking for '<tgName> <tgLastName>'"
                    EndIf
                    
                    cnt = 0
                    cntx = 0
                    srchType = choice
                    While iterate
                        cnt+= 1
                        cntx+=1
                        If cntx>= 30
                            "STATUS: Processing NPC <cnt> of many..."
                            cntx=0
                        EndIf
                        
                        test = 0 
                        If srchType == 202
                            test = getPerson()
                        ElseIf srchType == 203
                            test = getRelative()
                        EndIf
                        
                        If test.isValid()
                            tn = "<test.Name>"
                            If findAll || isSameString(tgName, tn)
                                
                                // Match last name
                                If doLastNameCheck
                                    tn_l = "<test.Name_Last>"
                                    If !isSameString(tn_l, tgLastName)
                                        test = 0
                                    EndIf
                                EndIf
                                
                                If test.isValid()
                                    secondScreen(test)
                                    test.show()
                                    
                                    "Possible match found: <test.Name> <test.name_last> (Age <test:age>), Use?"
                                    0::"Yes"
                                    1::"No"
                                    2::"Abort"
                                    
                                    If 0
                                        tgTemp = test
                                        iterate = false
                                    ElseIf 2
                                        tgTemp = 0
                                        iterate = false
                                    EndIf
                                    
                                    test.hide()
                                    secondScreen()
                                EndIf
                            EndIf
                        Else
                            iterate = false
                        EndIf
                    EndWhile
                    
                    If !tgTemp.isValid()
                        "Nothing found! Sorry. Searched <cnt> NPCs."
                    EndIf
                EndIf
            EndIf
            
            // "DEBUG: tgSlot=<tgSlot>, tgTemp=<tgTemp.Name>"
            If tgTemp.isValid()
                If tgSlot == 1
                    target = tgTemp
                Else
                    target2 = tgTemp
                EndIf
            EndIf
        ElseIf 10
            exchangeContact(target)
            "DONE! <target.Name> is now in your contacts!"
        ElseIf 20
            llt = getSpecific(Landlord)
            ll="none"
            If llt.isValid()
                ll = "<llt.Name> <llt.name_last>"
            EndIf
            
            bot = getSpecific(Boss)
            bo="none"
            If bot.isValid()
                bo = "<bot.Name> <bot.name_last>"
            EndIf
            
            cbt = getSpecific(CrimeBoss)
            cb="none"
            If cbt.isValid()
                cb = "<cbt.Name> <cbt.name_last>"
            EndIf
            
            sot = getSpecific(Dating)
            so="none"
            If sot.isValid()
                so = "<sot.Name> <sot.name_last>"
            EndIf
            
            ptt = getSpecific(PT)
            pt="none"
            If ptt.isValid()
                pt = "<ptt.Name> <ptt.name_last>"
            EndIf
            
            SugarB = SugarBabe.getGlobal()
            sbt = getSpecific(SugarB)
            st="none"
            If sbt.isValid()
                st = "<sbt.Name> <sbt.name_last>"
            EndIf
            
            99::false::"*** click entry to select the NPC as target 1***"
            0:: "Landlord of PLAYER: <ll>"
            1:: "Boss of PLAYER: <bo>"
            2:: "Crime Boss of PLAYER: <cb>"
            3:: "Significant other of PLAYER: <so>"
            4:: "Personal trainer of PLAYER: <pt>"
            5:: "SugarBabe of PLAYER: <st>"
            99:: "RETURN"
            
            If 0 
                target = llt
                "target1 = Landlord"
            ElseIf 1
                target = bot
                "target1 = Boss"
            ElseIf 2
                target = cbt
                "target1 = CrimeBoss"
            ElseIf 3
                target = sot
                "target1 = Dating"
            ElseIf 4
                target = ptt
                "target1 = PT"
            ElseIf 5
                target = sbt
                "target1 = SugarBabe"
            EndIf
        ElseIf 21
            If target.isValid()
                100:: "Make <target.Name> Landlord of PLAYER (replaces old one)"
                101:: "Make <target.Name> Boss of PLAYER (replaces old one)"
                102:: "Make <target.Name> Crime Boss of PLAYER (replaces old one)"
                103:: "Make <target.Name> significant other = dating of PLAYER (may replace old one)"
                104:: "Make <target.Name> Ex of PLAYER"
                105:: "Make <target.Name> Personal trainer of PLAYER (replaces old one)"
                110:: "Make <target.Name> Sugar babe of PLAYER (replaces old one)"
                0:: "RETURN"
                
                If 100
                    target.setLandlord()
                    "DONE. Landlord set to <target.Name>!"
                ElseIf 101
                    target.setBoss()
                    "DONE. Boss set to <target.Name>!"
                ElseIf 102
                    target.setCrimeBoss()
                    "DONE. Crime boss set to <target.Name>!"
                ElseIf 103
                    target.setDating()
                    "DONE. Date set to <target.Name>!"
                ElseIf 104
                    target.setExRelative(Player)
                    "DONE. ExRelative set to <target.Name>!"
                ElseIf 105
                    target.setPT()
                    "DONE. PT set to <target.Name>!"
                ElseIf 106  // not woring atm
                    target.setLivingWithPlayer(true)
                    "DONE. <target.Name> lives with the player!"
                ElseIf 107  // not working atm
                    target.setLivingWithPlayer(false)
                    "DONE. <target.Name> no more lives with the player!"
                ElseIf 110 
                    sid = target.getId()
                    SugarBabe.setGlobal(sid)
                    "DONE. <target.Name> is now your sugar babe!"
                EndIf
            Else
                "Select a valid target first!"
            EndIf
        ElseIf 22
            If target.isValid() && target.isRelative()
                221:: "Make <tg1> PLAYER's Child"
                222:: "Make <tg1> PLAYER's Sibling"
                223:: "Make <tg1> PLAYER's Cousin"
                224:: "Make <tg1> PLAYER's Parent"
                225:: "Make <tg1> PLAYER's GrandParent"
                226:: "Make <tg1> PLAYER's ParentSibling"
                227:: "Make <tg1> PLAYER's StepSibling"
                228:: "Make <tg1> PLAYER's StepChild"
                229:: "Make <tg1> PLAYER's StepParent"
                230:: !target.livesWithPlayer()::"Make <target.Name> living with PLAYER"
                231:: target.livesWithPlayer()::"Make <target.Name> stop living with PLAYER"
                0:: "RETURN"
                
                If 221
                    target.setRelativeType(Child)
                    "DONE. <target.Name> is now your Child."
                ElseIf 222
                    target.setRelativeType(Sibling)
                    "DONE. <target.Name> is now your Sibling."
                ElseIf 223
                    target.setRelativeType(Cousin)
                    "DONE. <target.Name> is now your Cousin."
                ElseIf 224
                    target.setRelativeType(Parent)
                    "DONE. <target.Name> is now your Parent."
                ElseIf 225
                    target.setRelativeType(GrandParent)
                    "DONE. <target.Name> is now your GrandParent."
                ElseIf 226
                    target.setRelativeType(ParentSibling)
                    "DONE. <target.Name> is now your ParentSibling."
                ElseIf 227
                    target.setRelativeType(StepSibling)
                    "DONE. <target.Name> is now your StepSibling."
                ElseIf 228
                    target.setRelativeType(StepChild)
                    "DONE. <target.Name> is now your StepChild."
                ElseIf 229
                    target.setRelativeType(StepParent)
                    "DONE. <target.Name> is now your StepParent."
                ElseIf 230
                    target.setLivingWithPlayer(true)
                    "DONE. <target.Name> is now living with you."
                ElseIf 231
                    target.setLivingWithPlayer(false)
                    "DONE. <target.Name> no more lives with you!"
                EndIf
            Else
                "Select a valid relative first!"
            EndIf
        ElseIf 23
            If target.isValid() && target2.isValid()
                231:: "Make <tg1> dating <tg2>"
                232:: "Make <tg1> and <tg2> spouses"
                233:: "Make <tg1> and <tg2> siblings"
                234:: "Make <tg1> parent of <tg2>"
                235:: "Make <tg1> and <tg2> cousins"
                236:: "Make <tg1> boss of <tg2>"
                237:: "Make <tg1> and <tg2> collegues"
                99::"RETURN"
                
                If 231
                    addNpcRelationship( Dating, target, target2 )
                    "DONE. <tg1> is now dating <tg2>."
                ElseIf 232
                    addNpcRelationship( Spouses, target, target2 )
                    "DONE. <tg1> and <tg2> are now spouses."
                ElseIf 233
                    addNpcRelationship( Siblings, target, target2 )
                    "DONE. <tg1> and <tg2> are now siblings."
                ElseIf 234
                    addNpcRelationship( ParentChild, target, target2 )
                    "DONE. <tg1> is now parent of <tg2>."
                ElseIf 235
                    addNpcRelationship( Cousins, target, target2 )
                    "DONE. <tg1> and <tg2> are now cousins."
                ElseIf 236
                    addNpcRelationship( BossEmployee, target, target2 )
                    "DONE. <tg1> is now boss of <tg2>."
                ElseIf 235
                    addNpcRelationship( Collegues, target, target2 )
                    "DONE. <tg1> and <tg2> are now collegues."
                EndIf
            Else
                "Select two valid NPC targets first!"
            EndIf
        ElseIf 31 || 32 || 33
            action = choice
            txtAction = ""
            If action == 31
                txtAction = "ERASING ALL NPCs! THIS CANNOT BE UNDONE. CONTINUE?"
            ElseIf action == 32
                txtAction = "ERASING ALL NON CONTACT NPCs! THIS CANNOT BE UNDONE. CONTINUE?"
            ELSE
                txtAction = "ERASING ALL RELATIVES! THIS CANNOT BE UNDONE. CONTINUE?"
            EndIf
            
            1::false::"<txtAction>"
            1::false::"------------------------------"
            0:: "Ooops. Wrong button! ABORT!"
            1::false::"------------------------------"
            2:: "I know what I am doing and made a backup! GO!"
            
            If 2
                count = 0
                contin = true
                While contin
                    tg = 0
                    If action == 31
                        tg = getPerson()
                    ElseIf action == 32
                        tg = getPerson(false)
                    Else
                        tg = getRelative()
                    EndIf
                    
                    If tg.isValid()
                        tg.DeletePerson()
                        count += 1
                    Else
                        contin = false
                    EndIf
                EndWhile
                
                "DONE! <count> NPCs DELETED."
            EndIf
        ElseIf 41   // Batch processing
            stayInSubMenu = true
            wantMale = 0
            wantFemale = 0
            wantTrans = 0
            wantPregnant = 0
            wantContact = 0
            wantProstitute = 0
            wantMates = 0
            selAction = 0 
            while stayInSubMenu
                
                txtMaleStat = "NOT SET"
                If wantMale == 1
                    txtMaleStat="ONLY"
                ElseIf wantMale == 2
                    txtMaleStat="EXCLUDE"
                EndIf
                
                txtFemaleStat = "NOT SET"
                If wantFemale == 1
                    txtFemaleStat="ONLY"
                ElseIf wantFemale == 2
                    txtFemaleStat="EXCLUDE"
                EndIf
                
                txtTransStat = "NOT SET"
                If wantTrans == 1
                    txtTransStat="ONLY"
                ElseIf wantTrans == 2
                    txtTransStat="EXCLUDE"
                EndIf
                
                txtPregStat = "NOT SET"
                If wantPregnant == 1
                    txtPregStat="ONLY"
                ElseIf wantPregnant == 2
                    txtPregStat="EXCLUDE"
                EndIf
                
                txtContactStat = "NOT SET"
                If wantContact == 1
                    txtContactStat="ONLY"
                ElseIf wantContact == 2
                    txtContactStat="EXCLUDE"
                EndIf
                
                txtProstituteStat = "NOT SET"
                If wantProstitute == 1
                    txtProstituteStat="ONLY"
                ElseIf wantProstitute == 2
                    txtProstituteStat="EXCLUDE"
                EndIf
                
                txtMateStat = "NOT SET"
                If wantMates == 1
                    txtMateStat="ONLY"
                ElseIf wantMates == 2
                    txtMateStat="EXCLUDE"
                EndIf
                
                txtSelAction = "LIST NPCs"
                If selAction == 1
                    txtSelAction = "TAG NPCs"
                ElseIf selAction == 2
                    txtSelAction = "UNTAG NPCs"
                ElseIf selAction == 3
                    txtSelAction = "ADD TO CONTACTS"
                ElseIf selAction == 4
                    txtSelAction = "DELETE NPCs"
                ElseIf selAction == 5
                    txtSelAction = "END PREGNANCY"
                ElseIf selAction == 6
                    txtSelAction = "MAKE PREGNANT FROM <tg2>"
                ElseIf selAction == 7
                    txtSelAction = "COUNT NPCs"
                EndIf
                
                410::"Male NPCs: <txtMaleStat>"
                411::"Female NPCs: <txtFemaleStat>"
                412::"Trans NPCs: <txtTransStat>"
                413::"Pregnant NPCs: <txtPregStat>"
                414::"Contacts: <txtContactStat>"
                415::"Sex workers: <txtProstituteStat>"
                416::"MC had sex with: <txtMateStat>"
                3::false::"----------------------------------"
                450::"Action: <txtSelAction>"
                3::false::"----------------------------------"
                498::"GO"
                499::"RETURN"
                
                subSelect = choice
                If 410
                    wantMale += 1
                    If wantMale > 2
                        wantMale = 0
                    EndIf
                ElseIf 411
                    wantFemale += 1
                    If wantFemale > 2
                        wantFemale = 0
                    EndIf
                ElseIf 412
                    wantTrans += 1
                    If wantTrans > 2
                        wantTrans = 0
                    EndIf
                ElseIf 413
                    wantPregnant += 1
                    If wantPregnant > 2
                        wantPregnant = 0
                    EndIf
                ElseIf 414
                    wantContact += 1
                    If wantContact > 2
                        wantContact = 0
                    EndIf
                ElseIf 415
                    wantProstitute += 1
                    If wantProstitute > 2
                        wantProstitute = 0
                    EndIf
                ElseIf 416
                    wantMates += 1
                    If wantMates > 2
                        wantMates = 0
                    EndIf
                ElseIf 450
                    selAction += 1
                    If selAction > 7
                        selAction = 0
                    EndIf
                ElseIf subSelect == 498
                    99::false::"PLEASE CONFIRM THAT YOU WANT TO: <txtSelAction>!"
                    99::false::"------------------------------"
                    0:: "Ooops. Wrong button! ABORT!"
                    99::false::"------------------------------"
                    2::"I know what I am doing and made a backup! GO!"
                    
                    If 2
                        clearGetList()
                        
                        npc = 0
                        count = 0
                        countx = 0
                        countm = 0 
                        iterate = true
                        While iterate
                            If wantContact == 1
                                npc = getPerson(true)
                            ElseIf wantContact == 2
                                npc = getPerson(false)
                            Else
                                npc = getPerson()
                            EndIf
                            
                            If npc.isValid()
                                countx += 1
                                count += 1
                                
                                If wantPregnant == 1
                                    If !npc.isPregnant()
                                        npc = 0
                                    EndIf
                                ElseIf wantPregnant == 2
                                    If npc.isPregnant()
                                        npc = 0
                                    EndIf
                                EndIf
                                
                                If wantMale == 1
                                    If !npc.isMale()
                                        npc = 0
                                    EndIf
                                ElseIf wantMale == 2
                                    If npc.isMale()
                                        npc = 0
                                    EndIf
                                EndIf
                                
                                If wantFemale == 1
                                    If npc.isMale()
                                        npc = 0
                                    EndIf
                                ElseIf wantFemale == 2
                                    If !npc.isMale()
                                        npc = 0
                                    EndIf
                                EndIf
                                
                                If wantTrans == 1
                                    If !npc.isTrans()
                                        npc = 0
                                    EndIf
                                ElseIf wantTrans == 2
                                    If npc.isTrans()
                                        npc = 0
                                    EndIf
                                EndIf
                                
                                If wantProstitute == 1
                                    If !npc.isProstitute()
                                        npc = 0
                                    EndIf
                                ElseIf wantProstitute == 2
                                    If npc.isProstitute()
                                        npc = 0
                                    EndIf
                                EndIf
                                
                                If wantMates == 1
                                    If !npc.hadSex()
                                        npc = 0
                                    EndIf
                                ElseIf wantMates == 2
                                    If npc.hadSex()
                                        npc = 0
                                    EndIf
                                EndIf
                                
                                If npc.isValid()
                                    countm += 1
                                    
                                    If selAction==0 // list
                                    
                                        secondScreen(npc)
                                        npc.show()
                                        
                                        "NPC match: <npc.name> <npc.name_last>, age <npc:age>."
                                        
                                        npc.hide()
                                    ElseIf selAction == 1   // tag
                                        npc.setActorVar(tag_NPCMGR, 1)
                                    ElseIf selAction == 2   // untag
                                        npc.setActorVar(tag_NPCMGR, 0)
                                    ElseIf selAction == 3   // contact
                                        If !npc.isContactExchanged()
                                            exchangeContact(npc)
                                        Else
                                            countm -= 1 // decrement match counter as did not really match
                                        EndIf
                                    ElseIf selAction == 4   // delete
                                        npc.deletePerson()
                                    ElseIf selAction == 5   // end pregnancy
                                        npc.endPregnancy()
                                    ElseIf selAction == 6   // impregnate
                                        npc.impregnate(target2)
                                    ElseIf selAction == 7   // Count
                                        // do nothing.
                                    EndIf
                                EndIf
                            Else
                                iterate = false
                            EndIf
                            
                            If countx >= 30
                                countx = 0
                                
                                99::false::"Processed <count> npcs, <countm> matched. Continue?"
                                0::"Yes"
                                1::"No"
                                
                                If 1
                                    iterate = false
                                EndIf
                            EndIf
                        EndWhile
                        
                        secondScreen()
                        "DONE! Processed <count> NPCs. <countm> matched the criteria."
                    EndIf
                Else
                    stayInSubMenu = false
                EndIf
            EndWhile
        Else
            stayInMenu = false
        EndIf
    EndWhile
    
    SceneEnd()