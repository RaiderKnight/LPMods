WHAT: none
WHERE: none
WHEN: 0 - 24
WHO: none
OTHER: none

    SceneStart()
    
    // Go to the office if any
    officeSize = Player.getActorVar(pe_PlayerOfficeLocationSize)
    If officeSize > 0
        moveTo(Home)
        setBackground3D(Modules/nn_PornEmpire/Rooms/nn_pe_office_1.lpworld)
    EndIf

    If Player.isNaked()
        Player.dress()
    EndIf
    Player.show()
    
    "Please select the model that you want to manage."
    selectNPC(PEModel)
    MainActor = getSelectedNPC()
    
    If MainActor.isValid()
        // MainActor.dressExcept(Outerwear, Top, Bottom, Foot, Headwear, Eyewear)
        MainActor.dress()
        MainActor.show(1)
        
        traningPrice = 3000 * Random(0.8 , 1)   // Some discount?
        traningPriceLocal = traningPrice.convertToLocalCurrency(true)
        
        Player::"What do I want to do?"
        
        stayInMenu = true
        While stayInMenu
            0:: "Check model's statistics"
            1:: "Book some ads to promote <MainActor.Name> for <traningPriceLocal>"
            2:: "Do a shooting with <MainActor.Name>"
            98:: "Select other model"
            99:: "Return"
            
            If 0
                fame = MainActor:pornfame
                cntSolo = MainActor.getActorVar(pe_SoloShootingsDone)
                cntGay = MainActor.getActorVar(pe_GayShootingsDone)
                cntLesbian = MainActor.getActorVar(pe_LesbianShootingsDone)
                cntAmateur = MainActor.getActorVar(pe_AmateurShootingsDone)
                cntThreesome = MainActor.getActorVar(pe_ThreesomeShootingsDone)
                
                isAgencyModel = MainActor.getActorVar(pe_IsAgencyModel)                  // 1 if the model was generated "by the agency"
                cntSets = MainActor.getActorVar(pe_VideoCount)                          // number of videos of this MainActor
                valSets = MainActor.getActorVar(pe_VideosValue)                         // value of the videos of this MainActor
                cntPendingSets = MainActor.getActorVar(pe_VideoCountPending)           // pending video uploads
                valPendingSets = MainActor.getActorVar(pe_VideosValuePending)           // value of the pending uploads
                sumSets = cntSets+cntPendingSets
                
                modelsBiEnabled = Player.getActorVar(pe_EnabledBiModels)
                
                fameText = "currently unknown"
                If fame >= 90
                    fameText = "a porn god"
                ElseIf fame >= 80
                    fameText = "famous"
                ElseIf fame >= 65
                    fameText = "very well known"
                ElseIf fame >= 50
                    fameText = "well known"
                ElseIf fame >= 35
                    fameText = "somewhat known"
                ElseIf fame >= 25
                    fameText = "little known"
                ElseIf fame >= 10
                    fameText = "very little known"
                EndIf
                
                txtModelType = "one of my models"
                If isAgencyModel>0
                    txtModelType = "a PornAgency model"
                EndIf
                
                txtGayLes = ""
                If MainActor.isMale()
                    // Generate a male character so we can test if the selected model is gay
                    tmpActor = generatePersonTemporary()
                    While !tmpActor.isMale()
                        tmpActor = generatePersonTemporary()
                    EndWhile
                    
                    If MainActor.isInterestedIn(tmpActor) || modelsBiEnabled>0
                        txtGayLes = "<MainActor.Name> has done <cntGay> gay shootings."
                    Else
                        txtGayLes = "<MainActor.Name> won't do gay shootings."
                    EndIf
                Else
                    // Generate a female character so we can test if the selected model is lesbian
                    tmpActor = generatePersonTemporary()
                    While tmpActor.isMale()
                        tmpActor = generatePersonTemporary()
                    EndWhile
                    
                    If MainActor.isInterestedIn(tmpActor) || modelsBiEnabled>0
                        txtGayLes = "<MainActor.Name> has done <cntLesbian> lesbian shootings."
                    Else
                        txtGayLes = "<MainActor.Name> won't do lesbian shootings."
                    EndIf
                    
                EndIf
                0:: "<MainActor.Name> is <txtModelType> and <fameText> in the porn industry."
                0:: "<MainActor.S> has done <cntAmateur> amateur and <cntSolo> solo shootings."
                0:: "<txtGayLes>"
                0:: "<MainActor.name> took part in <cntThreesome> threesome shootings."
                0:: "We have <sumSets> sets with <MainActor.o> as main actor."
                0:: "Click to continue..."
                
            ElseIf 1
                last = MainActor.getActorVar(pe_LastJobTraining)
                daysSinceBirth = (Player:Age * 365) // get the current date 
                delta = daysSinceBirth - last
                
                If delta < 7
                    timeToWait = 7 - delta
                    Player(Annoyed)::"I already booked ads for <MainActor.Name> this week. I will have to wait <timeToWait> days."
                Else
                    Player(Serene)::"<MainActor.Name> needs some more reputation to increase <MainActor.p> visitor numbers. Pay <traningPriceLocal> for some ads?"
                    0:: "Yes"
                    1:: "No"
                    
                    If 0
                        MainActor.setActorVar(pe_LastJobTraining, daysSinceBirth) // remember last training
                        
                        MainActor.dress()
                        MainActor.show(1)
                        
                        Random
                            Player(Serene)::"Listen, <MainActor.Name> I booked some ads for you to make you more famous."
                            Player(Serene)::"Hey <MainActor.Name>, I booked a new ad for web site featuring you as main actor!"
                        EndRandom
                        
                        money -= traningPrice
                        gain = Random(0.5,4)
                        MainActor:pornfame += gain
                        
                        If debugModeEnabled > 0 
                            gain *= 1000
                            "Debug TRANING: gain=<gain>/1000"
                        EndIf
                        
                        Random
                            MainActor(Serene)::"Awesome! Thanks for doing that for me!"
                            MainActor(Serene)::"That's cool. I want to be a real porn star!"
                            MainActor(Flirty)::"Nice. Hope I will attract many more visitors to your site!"
                        EndRandom
                    EndIf
                EndIf
            ElseIf 2
                MainActor.passOn()
                followUp(nn_scene_do_shooting)
                stayInMenu = false
            ElseIf 98
                
                // Back to model selection 
                followUp( nn_pe_scene_manage_models )
                stayInMenu = false
                
            ElseIf 99
                
                //Back to main menu
                followUp( nn_pe_scene_manage_employees )
                stayInMenu = false
            EndIf
        EndWhile
    EndIf
    
    passTime(1,2)
    
    SceneEnd()