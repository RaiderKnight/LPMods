WHAT: none
WHERE: none
WHEN: 0 - 24
WHO: none
OTHER: none

    SceneStart()
    
    If Player.isNaked()
        Player.dress()
    EndIf
    Player.show()
    
    GodMode = Player.getActorVar(pe_GodModeEnabled)
    
    MainActor = CurrentCompanion
    
    // Ask player if he intends to ask the companion
    If MainActor.isValid()
        Player::"Should I ask my companion <MainActor.Name> if <MainActor.s> is interested in working for me?"
        0:: "Yes"
        1:: "No"
        
        If 1
            MainActor.hide()
            MainActor = 0
        EndIf
    EndIf
    
    // Find a contact to ask
    If !MainActor.isValid()
        Player::"Let's check my contacts for someone I could ask to work as a web cam model for me."
        selectNPC()
        MainActor = getSelectedNPC()
    EndIf
    
    // Test if the selected actor is already employed
    If MainActor.isValid()
        currentSalary = MainActor.getActorVar(pe_EmployeeSalary)
        If currentSalary > 0 
            Player::"<MainActor.Name> is already working for me. I cannot offer <MainActor.o> another job!"
            MainActor.hide()
            MainActor = 0
        EndIf
    EndIf
    
    // <--- COPY MARKER, original: nn_pe_scene_ask_to_become_web_cam_model.lpscene
    // Ask the actor if he/she is interested
    If GodMode == 1
        // All done
    ElseIf MainActor.isValid() && !MainActor.isProstitute()
        hasDoneShootings = MainActor.getActorVar(pe_HasDoneShootings)
        noShootingsState = MainActor.getActorVar(pe_DeclinedDoingShootings)
        noWebCamState = MainActor.getActorVar(pe_DeclinedDoingWebCam)
        camPrice = MainActor.getActorVar(pe_EmployeeSalary)
        
        // If already camming
        If camPrice > 0 
            Player::"<MainActor.Name> is already doing live sex shows for me."
            MainActor = 0
        Else
            Player::"Let's see if <MainActor.name> wants to make some money..."
            
            MainActor.dress()
            MainActor.show()
            Player.moveToPersonStand(MainActor, 50) // Walk close to the target
            
            // If the actor is perverted enough he/she will agree
            If MainActor:perversion > 55 || hasDoneShootings > 0
                If hasDoneShootings > 0
                    Random
                        MainActor(Serene)::"Webcamming? You mean doing live sex on the internet? Hm. Sounds fun!"
                        MainActor(Grin)::"You mean you will pay me to jerk off to strangers? Hah! Count me in!"
                        MainActor(Serene)::"Earn even more money? Did you really ask that? Where is my computer?"
                        MainActor(Sad)::"I don't even have a computer! What? You will get me one?"
                        MainActor(Posing)::"I already have some experience. Only private skype you know? I'm happy to work for you."
                    EndRandom
                Else
                    Random
                        MainActor(Sarcastic)::"Just show my ass on cam and earn money? Ok, count me in!"
                        MainActor(Shy)::"Do you think I could do that? Ok. I'll give it a try."
                        MainActor(Posing)::"I recently saw some of these shows and was thinking: that could be me! Ok, I'll give it a go!"
                        MainActor(Surprised)::"Wow. I did not think about this yet, but now as you ask: why not. Easy money, right?"
                        MainActor(Sarcastic)::"Oh yeah! Now I know what I was missing all the time: some audience while masturbating!!"
                    EndRandom
                EndIf
            Else
                If MainActor:masochist < -50
                    If noWebCamState == 1
                        Random
                            MainActor(Embarassed)::"You are not giving up easily hm? I am still not convinced."
                            MainActor(Shocked)::"No, I already told you that I am the wrong person for such stuff."
                            MainActor(Shocked)::"I saw that question coming. Still no!"
                            MainActor(Irritated)::"No. Really. I do not want to do that."
                        EndRandom
                    ElseIf noShootingsState > 0
                        Random
                            MainActor(Irritated)::"So you could not make me do a shooting with you and believe I do live sex?"
                            MainActor(Irritated)::"Erm, what? I did not want to do shootings and now you ask for that??"
                            MainActor(Sarcastic)::"Yeah, you were sooo successful making me a model. Right?"
                            MainActor(Sarcastic)::"WTF! I did not want to be a model. Why should I do live sex?"
                        EndRandom
                    Else
                        Random
                            MainActor(Embarassed)::"I don't want everyone on the internet to see me naked."
                            MainActor(Shy)::"Ooooh no! I'll loose my job and home if I do that!"
                            MainActor(Shocked)::"What? Live sex? You must be kidding!"
                            MainActor(Irritated)::"Erm, you know what you just asked right? I need to think about that..."
                            MainActor(Confused)::"Did you just ask me to get naked? In front of a camera? I never did such thing."
                        EndRandom
                    EndIf
                    
                    Random
                        Player(Curious)::"Seems <Actor.Name> is a little bit shy on that topic."
                        Player(Curious)::"Well, some more teasing and I have a new model..."
                        Player(Curious)::"<Actor.Name> needs a little more pampering and will be ready to go..."
                    EndRandom
                    
                    Random
                        Player(Curious)::"I think I could talk <Actor.o> into doing it."
                        Player(Wink)::"Should I try to convince <MainActor.Name> to work for me?"
                        Player(Wink)::"I see a chance I could make <MainActor.Name> change <Actor.p> mind. Use it?"
                    EndRandom
                    0:: "Yes"
                    1:: "No"
                    
                    If 0
                        Random
                            Player(Wink)::"Hey I know that you are in debt. You have a nice body. Just start making cash!"
                            Player(Wink)::"Why not pay your rent while you are at home? I'll give you a computer and you try it out..."
                            
                            // TODO: Write some unique statements. Those are copied from PhotoShoot
                            Player(Flirty)::"Listen honey, you are so beautiful. Not showing your body to the world would be a sin."
                            Player(Flirty)::"You are such a beautiful and sexy person. You really should give it a try!"
                            Player(Wink)::"I know that you, always wanted to try this. Now is your chance. Take it fast."
                            Player(Wink)::"There is nothing wrong with being naked in front of a camera. It's like being at a nude beach."
                            Player(Wink)::"Let's say I would give you some hundreds cash - Still not interested?"
                        EndRandom
                        
                        chance = Random(1,4)
                        
                        If chance >= 3
                            Random
                                MainActor(Anxious)::"I really need some money to pay my rent. I will do it. But only one month!"
                                MainActor(Excited)::"Ok. I'm interested if the price is right!"
                                MainActor(Shy)::"Sounds like easy money. Count me in!"
                                MainActor(Excited)::"Looks like I'll have something to do the next evenings right?"
                            EndRandom
                        Else
                            Random
                                MainActor(Angry)::"Do you know what happens if my boss learns about that? I need my job, so no. Sorry!"
                                MainActor(Angry)::"If my friend finds out about that I am doomed! I can't do that!"
                                MainActor(Irritated)::"Nah, I don't want the world to watch me while I do that. I feel dirty enough touching myself."
                                MainActor(Angry)::"Did you just try to talk me into your dirty porn business? NO!"
                                MainActor(Irritated)::"I think I am not really ready for that. Give me some time..."
                            EndRandom
                            
                            MainActor.setActorVar(pe_DeclinedDoingWebCam, 1)
                            
                            // Not taking part
                            MainActor = 0
                        EndIf
                    Else
                        // Not taking part
                        MainActor = 0
                    EndIf
                Else
                    Random
                        MainActor(Angry)::"What? Do you think I am a cheap slut? Forget it!"
                        MainActor(Angry)::"You must be kidding. This is not going to happen. Believe me!"
                        MainActor(Shocked)::"What? I? Sure not! Go find someone else!"
                        MainActor(Shocked)::"What are you talking about? Are you crazy? No way!"
                        MainActor(Angry)::"Sure, I'll risk all my life for your porn stuff. Find someone else OK?"
                    EndRandom
                    
                    Random
                        Player(Annoyed)::"All right, seems <MainActor.Name> really is not going to work for me."
                        Player(Annoyed)::"Oops... I better find someone else to work for me..."
                        Player(Sad)::"Damn! That was a definitive 'NO'. Luckily there are more people around."
                        Player(Sad)::"Fuck... I will have to look for someone else."
                        Player(Sarcastic)::"Hah! <MainActor.S> will come bagging some day..."
                        Player(Sarcastic)::"Hm, did I really expect <MainActor.Name> to say yes?"
                    EndRandom
                    
                    // Not taking part
                    MainActor.setActorVar(pe_DeclinedDoingWebCam, 2)
                    MainActor = 0
                EndIf
                
            EndIf
        EndIf
    EndIf
    
    askingPrice = 0
    askingPriceLocal = 0
    If MainActor.isValid()
        
        // does porn...
        MainActor.setActorVar(pe_DeclinedDoingWebCam, 99)
        
        // "Debug: Calculate askingPrice intelligence=<MainActor:intelligence> attractiveness=<MainActor:attractiveness>"
        
        // Calculate salary
        If MainActor.isProstitute()    // prostitutes get a lower salary
            askingPrice = 200
            MainActor.setActorVar(pe_EmployeeSalary, askingPrice)
            askingPriceLocal = askingPrice.convertToLocalCurrency(true)
            
            Player(Serene)::"<MainActor.Name> is one of my sex workers. I will pay <MainActor.o> <askingPriceLocal> per cam session."
        Else
            // Cam price is based on attractiveness and intelligence
            askingPrice = 3.5 * MainActor:intelligence
        
            If MainActor:attractiveness > 50
                askingPrice = 8 * MainActor:intelligence
            ElseIf MainActor:attractiveness > 75
                askingPrice = 12 * MainActor:intelligence
            ElseIf MainActor:attractiveness > 90
                askingPrice = 16 * MainActor:intelligence
            EndIf
        
            MainActor.setActorVar(pe_EmployeeSalary, askingPrice)
            askingPriceLocal = askingPrice.convertToLocalCurrency(true)
            
            Player(Serene)::"<MainActor.Name> is asking for <askingPriceLocal> per cam session."
            0:: "Accept"
            1:: "Try to negotiate"
            
            If 1
                chance = Random(1,5)
                If chance >= 3
                    askingPrice -= (chance*10)
                    If askingPrice < 100
                        askingPrice = 100
                    EndIf
                    askingPriceLocal = askingPrice.convertToLocalCurrency(true)
                    Player(Wink)::"I was able to talk <MainActor.Name> down to <askingPriceLocal>."
                    
                    MainActor.setActorVar(pe_EmployeeSalary, askingPrice)
                Else
                    Random
                        Player(Annoyed)::"<MainActor.Name> was not willing to negotiate."
                        Player(Annoyed)::"I was out of luck trying to negotiate with <MainActor.Name>."
                        Player(Annoyed)::"<MainActor.Name> did not accept my request to pay less for <MainActor.p> services."
                    EndRandom

                    Player(Annoyed)::"I will have to pay what <MainActor.s> demands or find another model that I can afford."
                    0:: "Pay what <MainActor.s> demands"
                    1:: "Ask someone else"
                    
                    If 1
                        MainActor = 0
                    EndIf
                EndIf
            EndIf
        EndIf
    EndIf
    
    // Find a schedule (weekly)
    webcammingFrequency = 1
    If MainActor.isValid()
        
        Random
            Player(Serene)::"How often should <MainActor.Name> be on line? (<askingPriceLocal>/session)"
            Player(Serene)::"<MainActor.Name> has a lot of spare time. How often do I want to be <MainActor.o> to be on-line? (<askingPriceLocal>/session)"
        EndRandom
        
        1:: "Once a week"
        2:: "Twice a week"
        3:: "Three times a week"
        4:: "Four times a week"
        5:: "Five times a week"
        7:: "Each day of the week"
        
        webcammingFrequency = choice
        
        MainActor.setActorVar(pe_WebCammingFrequency, webcammingFrequency)
        MainActor.setActorVar(tag_PEWebCam, 1)
        
        cost = askingPrice * webcammingFrequency
        costLocal = cost.convertToLocalCurrency(true)
        
        Player(Serene)::"We agreed on a webcam schedule and session price."
        Player(Serene)::"I will pay <costLocal> for <webcammingFrequency> sessions per week."
        Player(Serene)::"<MainActor.Name> will start working today."
        
    EndIf
    // ---> COPY MARKER
    
    passTime(1, 2)
    
    followUp(nn_pe_scene_manage_web_cam_models)
    
    SceneEnd()