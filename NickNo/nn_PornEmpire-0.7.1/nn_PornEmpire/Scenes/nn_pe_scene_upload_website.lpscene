WHAT: none
WHERE: none
WHEN: 0 - 24
WHO: none
OTHER: none

    SceneStart()
    
    If Player:nnOpenedPornEmpire >= 1

        // Go to the office if any
        officeSize = Player.getActorVar(pe_PlayerOfficeLocationSize)
        If officeSize > 0
            moveTo(Home)
            setBackground3D(Modules/nn_PornEmpire/Rooms/nn_pe_office_1.lpworld)
        EndIf
        
        // Player.dress()
        
        detailModeEnabled = Player.getActorVar(pe_EnabledDetailMode)
        
        If Player:nnOpenedPornWebsite >= 1
            
            // If the player is dating someone we need to end the date, so we can check this NPCs stats as well
            If CurrentCompanion.isValid()
                Player::"<CurrentCompanion.Name>, I need to take care of my business. See you later!"
                endDate()
            EndIf
            
            // Iterate through all NPCs and check those with videos 
            pendingVideoCount = 0          // Videos to be uploaded
            
            Actor = getPerson(tag_PEModel)
            While Actor.isValid()
            
                // Get the number of pending uploads 
                pendingUploads = Actor.getActorVar(pe_VideoCountPending)
                
                If pendingUploads > 0
                    pendingVideoCount += pendingUploads
                    
                    // Add the pending videos to the released ones
                    setsCurrent = Actor.getActorVar(pe_VideoCount)
                    setsCurrent += pendingUploads
                    Actor.setActorVar(pe_VideoCount, setsCurrent)
                    
                    // Get the value of the pending uploads
                    actorShootVal = Actor.getActorVar(pe_VideosValue)
                    plusShootVal = Actor.getActorVar(pe_VideosValuePending)
                    actorShootVal += plusShootVal
                    Actor.setActorVar(pe_VideosValue, actorShootVal)
                    
                    // Reset pending
                    Actor.setActorVar(pe_VideoCountPending,0)
                    Actor.setActorVar(pe_VideosValuePending, 0)
                    
                    If detailModeEnabled > 0
                        secondScreenIfHidden(Actor)
                        // Actor.dress() // Lets keep them naked for now... is faster and looks better ^^
                        Actor.show()
                        
                        Random
                            Player::"I have uploaded <pendingUploads> new videos of <Actor.Name>. Let's see how these perform tomorrow."
                            Player::"I added <pendingUploads> videos of <Actor.Name>. Hope our visitors enjoy <Actor.o>."
                            Player::"<pendingUploads> new videos from <Actor.Name> added. Our horny users will be happy."
                        EndRandom
                        
                        secondScreen()
                        Actor.hide()
                        Actor.saveAndDelete()
                    EndIf
                    
                    passTime(0.1, 0.3)      // every video consumes time and energy
                    Player:energy -= Random(2,5)
                EndIf
                Actor = getPerson(tag_PEModel)
            EndWhile
        Else
            Player(Annoyed)::"Without a porn website uploading does not make any sense uh?"
            Player(Excited)::"I need to get my website up and running ASAP."
        EndIf
        
        If pendingVideoCount > 0 
            Player(Happy)::"I have uploaded <pendingVideoCount> new videos."
            
            currentAdvertisingMode = Player.getActorVar(pe_OnlineAdvertisingMode)
            If currentAdvertisingMode < 1
                Random
                    Player(Curious)::"Maybe I should do some advertising as well?"
                    Player(Curious)::"If I book some ads, I might increase the number of visitors..."
                    Player(Curious)::"Some online advertising could help increase our visitor number."
                EndRandom
            EndIf
        Else
            Player(Sad)::"Nothing to upload this time. I need to do more shootings."
        EndIf
    EndIf
    
    SceneEnd()